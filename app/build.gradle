plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.diego.discoteca"
        minSdk 23
        targetSdk 33
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        Properties properties = new Properties()
        properties.load(project.rootProject.file("local.properties").newDataInputStream())

        buildConfigField "String", "API_KEY", "\"${properties.getProperty("API_KEY")}\""
        buildConfigField "String", "API_SECRET", "\"${properties.getProperty("API_SECRET")}\""

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":
                                      "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled = true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += ["-opt-in=kotlin.RequiresOptIn"]
    }
    buildFeatures {
        dataBinding true
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES']
        }
    }

    namespace 'com.diego.discoteca'
}

dependencies {

    /* A failure occurred while executing app:checkDebugDuplicateClasses, Android Studio Electric Eel | 2022.1.1
     with org.jetbrains.kotlin:kotlin-reflect:1.8.0
     https://stackoverflow.com/questions/75274720/a-failure-occurred-while-executing-appcheckdebugduplicateclasses*/
    modules {
        module('org.jetbrains.kotlin:kotlin-stdlib-jdk8') {
            replacedBy 'org.jetbrains.kotlin:kotlin-stdlib', 'kotlin fixes'
        }
        module('org.jetbrains.kotlin:kotlin-stdlib-jdk7') {
            replacedBy 'org.jetbrains.kotlin:kotlin-stdlib', 'kotlin fixes'
        }
    }
    // Or : implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.0"))

    implementation "androidx.core:core-ktx:1.9.0"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    debugImplementation 'androidx.fragment:fragment-testing:1.5.5'

    // Core library for tests
    debugImplementation "androidx.test:core:1.5.0"

    testImplementation 'junit:junit:4.12'
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"
    testImplementation 'org.mockito:mockito-core:5.0.0'
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.1.0"

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.5.1"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"
    androidTestImplementation 'org.mockito:mockito-core:5.0.0'
    androidTestImplementation "com.linkedin.dexmaker:dexmaker-mockito:2.28.3"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.5"
    androidTestImplementation "androidx.navigation:navigation-testing:2.5.3"
    androidTestImplementation 'androidx.test:rules:1.5.0'

    // Timber
    implementation "com.jakewharton.timber:timber:$timberVersion"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    // Kotlin extensions for activity and fragments
    implementation "androidx.fragment:fragment-ktx:$fragmentVersion"
    implementation "androidx.activity:activity-ktx:$activityVersion"

    // Room
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"

    // Kotlin Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"

    // RecyclerView
    implementation "androidx.recyclerview:recyclerview:$recyclerViewVersion"

    // Android architecture components
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleExtensionsVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"

    implementation "org.jetbrains.kotlin:kotlin-reflect:$reflectVersion"

    // Barcode scanner
    implementation "com.google.mlkit:barcode-scanning:$mlkitVersion"

    // CameraX
    implementation "androidx.camera:camera-core:$cameraxVersion"
    implementation "androidx.camera:camera-camera2:$cameraxVersion"
    implementation "androidx.camera:camera-lifecycle:$cameraxVersion"
    implementation "androidx.camera:camera-view:$cameraxVersion"
    implementation "androidx.camera:camera-extensions:$cameraxVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpLoggingInterceptorVersion"

    // Moshi
    implementation "com.squareup.moshi:moshi-kotlin:$moshiVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$moshiConverterVersion"

    // Google Sign-in
    implementation "com.google.android.gms:play-services-auth:$playServiceVersion"

    // Google Drive Api
    implementation "com.google.api-client:google-api-client-android:$googleApiClientVersion"
    implementation "com.google.apis:google-api-services-drive:$googleApiServicesDriveVersion"

    // Splashscreen
    implementation "androidx.core:core-splashscreen:$splashscreenVersion"

    // Glide
    implementation "com.github.bumptech.glide:glide:$glideVersion"

    // DataStore Preferences
    implementation("androidx.datastore:datastore-preferences:$dataStoreVersion")

    // ZoneDateTime
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:$zoneDateTimeVersion")

    // Paging3
    implementation "androidx.paging:paging-runtime-ktx:$pagingVersion"
    implementation "androidx.room:room-paging:$roomVersion"
}